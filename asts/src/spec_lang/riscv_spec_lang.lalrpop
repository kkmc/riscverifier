// TODO(kkmc): Remove the dependency on DwarfCtx from the parser

use std::collections::{HashSet, HashMap};

use crate::spec_lang::sl_ast as ast;
use crate::spec_lang::sl_lexer::{Tok, LexError};

/// A grammar defined with
/// (1) an input string of the specification `input`,
/// (2) `curr_fn` is a stateful variable that keeps track of which function
///     the parser is currently parsing specifications for.
///     `curr_fn` is set whenever a new FuncSpec is parsed
grammar<'input>(input: &'input str, curr_fn: &mut String, typ_map: &mut HashMap<String, ast::VType>);

pub Spec: ast::Spec = {
    "ensures" <e:BExpr> ";" => ast::Spec::Ensures(e),
    "requires" <e:BExpr> ";" => ast::Spec::Requires(e),
    "modifies" <ids:ModSet> ";" => ast::Spec::Modifies(ids.iter().map(|s| s.to_string()).collect()),
    "track" "[" <id:"id"> "]" <e:VExpr> ";" => ast::Spec::Track(id.to_string(), e),
}

pub ModSet: HashSet<&'input str> = {
    <mut ms: ModSet> "," <id: "id"> => {
        ms.insert(id);
        ms
    },
    "id" => {
        let mut ms = HashSet::new();
        ms.insert(<>);
        ms
    },
}

pub Specs: Vec<ast::Spec> = {
    <Spec*> => <>,
}

pub FuncSpec: ast::FuncSpec = {
    "fun" <id:FuncId> "{" <ss:Specs> "}" => {
        ast::FuncSpec{fname: id, specs: ss}
    },
}

pub FuncId: String = {
    "id" => {
        *curr_fn = <>.to_string();
        <>.to_string()
    }
}

pub FuncSpecs: Vec<ast::FuncSpec> = {
    <FuncSpec*> => <>,
}

pub BExpr: ast::BExpr = {
    <e1:BExpr> <op:InfixBoolOp> <e2:BExpr2> => ast::BExpr::BOpApp(op, vec![e1, e2]),
    BExpr2,
}

pub BExpr2: ast::BExpr = {
    <op:PrefixBoolOp> <e:BExpr2> => ast::BExpr::BOpApp(op.clone(), vec![e]),
    BTerm,
}

pub BTerm: ast::BExpr = {
    <e1:VExpr> <op:CompOp> <e2:VExpr> => {
        let operands = vec![e1, e2];
        ast::BExpr::COpApp(op, operands)
    },
    "true" => ast::BExpr::Bool(true),
    "false" => ast::BExpr::Bool(false),
    "(" <e: BExpr> ")" => e,
}

pub VarDecl: (ast::VExpr, ast::VType) = {
    <v:"id"> ":" <typ:TypeDecl> => {
        (ast::VExpr::Ident(v.to_string(), typ.clone()), typ)
    },
}

pub TypeDecl: ast::VType = {
    "bv_type" => {
        match <> {
            Tok::BvType(width) => ast::VType::Bv(width),
            _ => panic!("Not a valid type declaration: {:#?}.", <>),                
        }
    }
}

pub InfixBoolOp: ast::BoolOp = {
    "||" => ast::BoolOp::Disj,
    "&&" => ast::BoolOp::Conj,
    "=" "=" ">" => ast::BoolOp::Implies,
}

pub PrefixBoolOp: ast::BoolOp = {
    "!" => ast::BoolOp::Neg,
    "forall" "(" <vd: VarDecl> ")" "::" => {
        let (var, typ) = vd;
        ast::BoolOp::Forall(var, typ)
    },
    "exists" "(" <vd: VarDecl> ")" "::" => {
        let (var, typ) = vd;
        ast::BoolOp::Exists(var, typ)
    },
}

pub CompOp: ast::CompOp = {
    ">" => ast::CompOp::Gt,
    "<" => ast::CompOp::Lt,
    ">" "=" => ast::CompOp::Geq,
    "<" "=" => ast::CompOp::Leq,
    ">" "_u" => ast::CompOp::Gtu,
    "<" "_u" => ast::CompOp::Ltu,
    ">" "=" "_u" => ast::CompOp::Geu,
    "<" "=" "_u" => ast::CompOp::Leu,
    "=" "=" => ast::CompOp::Equal,
    "!" "=" => ast::CompOp::Nequal,
}

pub VExpr: ast::VExpr = {
    <e1:VExpr> <op:ValueOp1> <e2:VExpr2> => {
        let operands = vec![e1, e2];
        ast::VExpr::OpApp(op, operands, ast::VType::Unknown)
    },
    VExpr2,
}

// Last precedence value operators
pub ValueOp1: ast::ValueOp = {
    "+" => ast::ValueOp::Add,
    "-" => ast::ValueOp::Sub,
    "^" => ast::ValueOp::BvXor,
    "&" => ast::ValueOp::BvAnd,
    "|" => ast::ValueOp::BvOr,
}

pub VExpr2: ast::VExpr = {
    <e1:VExpr2> <op:ValueOp2> <e2:Term> => {
        let operands = vec![e1, e2];
        ast::VExpr::OpApp(op, operands, ast::VType::Unknown)
    },
    <a:VExpr2> "[" <ind:VExpr> "]" => {
        let op = ast::ValueOp::ArrayIndex;
        let operands = vec![a, ind];
        ast::VExpr::OpApp(op, operands, ast::VType::Unknown)
    },
    <e:VExpr2> <op:PostfixValueOp> => {
        let operands = vec![e];
        ast::VExpr::OpApp(op, operands, ast::VType::Unknown)
    },
    <s:VExpr2> "." <f:"id"> => {
        assert!(f.to_string().chars().all(char::is_alphanumeric), "Invalid get field .{}.", f.to_string());
        let op = ast::ValueOp::GetField;
        let operands = vec![s, ast::VExpr::Ident(f.to_string(), ast::VType::Unknown)];
        ast::VExpr::OpApp(op, operands, ast::VType::Unknown)
    },
    Term,
}

pub PostfixValueOp: ast::ValueOp = {
    "[" <hi:"int"> ":" <lo:"int"> "]" => {
        if let Tok::Int(lo_u64) = lo {
            if let Tok::Int(hi_u64) = hi {
                ast::ValueOp::Slice{lo: lo_u64 as u16, hi: hi_u64 as u16}
            } else {
                panic!("Invalid right (high) slice value.");
            }
        } else {
            panic!("Invalid left (low) slice value.");
        }
    },
}

// Second last precedence value operators
pub ValueOp2: ast::ValueOp = {
    "/" => ast::ValueOp::Div,
    "*" => ast::ValueOp::Mul,
    ">>" => ast::ValueOp::RightShift,
    ">>>" => ast::ValueOp::URightShift,
    "<<" => ast::ValueOp::LeftShift,
    "++" => ast::ValueOp::Concat,
}

pub Term: ast::VExpr = {
    "bool" => {
        if let Tok::Bool(b) = <> {
            ast::VExpr::Bool(b, ast::VType::Bool)
        } else {
            panic!("Implementation error: bool should be Tok::Bool(..) enum type.")
        }
    },
    <m:"-"?> <i:"int"> => {
        if let Tok::Int(i) = i {
            let si = if m.is_some() {
                -i
            } else {
                i
            };
            ast::VExpr::Int(si, ast::VType::Int)
        } else {
            panic!("Implementation error: bool should be Tok::Int(..) enum type.")
        }
    },
    <m:"-"?> <bv:"bv"> => {
        if let Tok::Bv { value, width } = bv {
            let value = if m.is_some() {
                (-(value as i64)) as u64
            } else {
                value
            };
            ast::VExpr::Bv{value, typ: ast::VType::Bv(width)}
        } else {
            panic!("Implementation error: bool should be Tok::Bv(..) enum type.")
        }
    },
    <f:BuiltIn> "(" <es:CommaSepVExprs> ")" => {
        ast::VExpr::FuncApp(f, es, ast::VType::Unknown)
    },
    <d:"*"?> <id:Ident> => {
        if d.is_some() {
            ast::VExpr::OpApp(ast::ValueOp::Deref, vec![id.clone()], id.typ().clone())
        } else {
            id
        }
    },
    "(" <e:VExpr> ")" => e,
}

pub CommaSepVExprs: Vec<ast::VExpr> = {
    <mut vexprs: CommaSepVExprs> "," <e: VExpr> => {
        vexprs.push(e);
        vexprs
    },
    VExpr => {
        vec![<>]
    },
}

pub BuiltIn: String = {
    "old" => "old".to_string(),
    "sext" => "sext".to_string(),
    "uext" => "uext".to_string(),
}

pub Ident: ast::VExpr = {
    <d:"$"?> <id:"id"> => match d {
        Some(d) => {
            let var_id = id.to_string();
            ast::VExpr::Ident(id.to_string(), ast::VType::Unknown)
        },
        None => {
            let var_id = id.to_string();
            ast::VExpr::Ident(format!("{}", id), ast::VType::Unknown)
        },
    }
}

extern {
    type Location = usize;
    type Error = LexError;
    
    enum Tok<'input> {
        // Keywords;
        "ensures" => Tok::Ensures,
        "requires" => Tok::Requires,
        "modifies" => Tok::Modifies,
        "track" => Tok::Track,
        "fun" => Tok::Fun,
        "true" => Tok::True,
        "false" => Tok::False,
        "old" => Tok::Old,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "_u" => Tok::UnsignedCompExt,
        // Builtin functions
        ">>" => Tok::RightShift,    // >>
        ">>>" => Tok::URightShift,  // >>>
        "<<" => Tok::LeftShift,     // <
        "sext" => Tok::SignedExt,   // sext
        "uext" => Tok::UnsignedExt, // uext
        "++" => Tok::Concat,        // ++
        // Identifier
        "id" => Tok::Id(<&'input str>),
        // Primitives
        "int" => Tok::Int(i64),
        "bv" => Tok::Bv { value: u64, width: u16 },
        "bool" => Tok::Bool(bool),
        // Types
        "bv_type" => Tok::BvType(u16),
        // Symbols
        ":" => Tok::Colon,          // :
        "::" => Tok::ColonColon,    // ::
        ";" => Tok::Semi,           // ;
        "," => Tok::Comma,          // ,
        "." => Tok::Dot,            // .
        "=" => Tok::Equals,         // =
        ">" => Tok::GreaterThan,    // >
        "<" => Tok::LessThan,       // <
        "+" => Tok::Plus,           // +
        "-" => Tok::Minus,          // -
        "?" => Tok::Question,       // ?
        "*" => Tok::Asterisk,       // *
        "/" => Tok::Slash,          // /
        "&" => Tok::Ampersand,      // &
        "&&" => Tok::DoubleAmpersand, // &&
        "|" => Tok::Pipe,           // |
        "||" => Tok::DoublePipe,    // ||
        "~" => Tok::Tilde,          // ~
        "!" => Tok::Bang,           // !
        "^" => Tok::Caret,          // ^
        "$" => Tok::Dollar,         // $
        "{" => Tok::LeftBrace,      // {
        "[" => Tok::LeftBracket,    // [
        "(" => Tok::LeftParen,      // (
        "}" => Tok::RightBrace,     // }
        "]" => Tok::RightBracket,   // ]
        ")" => Tok::RightParen,     // )
    }
}